!!----------------------------
!! APDL IMPLEMENTATION OF
!! UNIFORMLY DISTRIBUTED LOADS
!!----------------------------
/PREP7
L=1
B=0.04
H=0.1
YM=20E9
NU=0.3
NELEM=10
P=8000
q=10000 !-Distributed load
!!------------------------------
!! MATERIAL PROPERTIES - BEAM188
!!------------------------------
ET,1,BEAM188,,,2  
MPTEMP,,,,,,,,  
MPTEMP,1,0  
MPDATA,EX,1,,YM  
MPDATA,PRXY,1,,NU
!!----------
!! SECTION
!!---------- 
SECTYPE,1,BEAM,RECT,,0   
SECOFFSET,CENT 
SECDATA,B,H,0,0,0,0,0,0,0,0,0,0
!!----------------------
!! KEYPOINTS AND LINES
!!----------------------
K,1,0,0,0
K,2,L,0,0
K,3,2*L,0,0
K,4,3*L,0,0
K,5,4*L,0,0
L,1,2
L,2,3
L,3,4
L,4,5
!!-------
!! MESH
!!-------
LESIZE,ALL,,,NELEM,,1,,,1,
LMESH,ALL
!!----------------------
!! BOUNDARY CONDITIONS
!!----------------------
NSEL,S,LOC,X,0,0 !-SELECT NODE AT LEFT END
D,ALL,UY,0
D,ALL,UZ,0
D,ALL,ROTX,0
D,ALL,ROTY,0
NSEL,S,LOC,X,4*L,4*L !-SELECT NODE AT RIGHT END
D,ALL,UX,0
D,ALL,UY,0
D,ALL,UZ,0
D,ALL,ROTX,0
D,ALL,ROTY,0
NSEL,S,LOC,X,0,0 !-SELECT NODE AT LEFT END TO APPLY MOMENT
F,ALL,MZ,P*L
NSEL,S,LOC,X,L,L !-SELECT NODE AT LEFT END TO APPLY POINT LOAD
F,ALL,FY,P
ESEL,S,CENT,X,2*L,4*L 
SFBEAM,ALL,2,PRES,q,,,,,,0
ALLSEL,ALL
FINISH  
!!----------
!! SOLUTION
!!----------
/SOLU
ANTYPE,STATIC
OUTRES,ALL,ALL
SOLVE
FINISH
!!-----------------
!! POST-RPOCESSING
!!-----------------
/POST1
SET,FIRST
! SHEAR FORCE AND BENDING MOMENT
ETABLE,MZI,SMISC,3
ETABLE,MZJ,SMISC,16
ETABLE,VYI,SMISC,6
ETABLE,VYJ,SMISC,19   